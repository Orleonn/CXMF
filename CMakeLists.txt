cmake_minimum_required(VERSION 3.28.0)
cmake_policy(SET CMP0077 NEW)

project(cxmf VERSION 1.0.0 LANGUAGES CXX)

option(CXMF_BUILD_ZLIB "Build zlib" ON)
option(CXMF_INCLUDE_IMPORTER "Include glTF 2.0 importer (includes assimp, glm, meshoptimizer)" ON)



set(CXMF_IS_STANDALONE_BUILD OFF)
if(${CMAKE_SOURCE_DIR} STREQUAL ${CMAKE_CURRENT_SOURCE_DIR})
	set(CXMF_IS_STANDALONE_BUILD ON)
endif()



set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)
set(CMAKE_C_STANDARD 11)
set(CMAKE_C_STANDARD_REQUIRED ON)
set(CMAKE_C_EXTENSIONS OFF)

set(SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/src)
set(INCLUDE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/include)
set(LIBRARIES_DIR ${CMAKE_CURRENT_SOURCE_DIR}/third_party)

set(BUILD_SHARED_LIBS OFF)
set(SKIP_INSTALL_LIBRARIES ON)
set(SKIP_INSTALL_ALL ON)
set(SKIP_INSTALL_HEADERS ON)
set(SKIP_INSTALL_FILES ON)

if(NOT CXMF_BUILD_ZLIB)
	find_package(zlib)
endif()

if(NOT ZLIB_FOUND AND NOT CXMF_BUILD_ZLIB)
	message(FATAL_ERROR "CXMF_BUILD_ZLIB is OFF but unable to find zlib dependency via 'find_package'")
elseif(NOT ZLIB_FOUND)
	set(ZLIB_BUILD_EXAMPLES OFF)
	set(ZLIB_FOUND ON)
	set(ZLIB_LIBRARIES zlibstatic)
	set(ZLIB_INCLUDE_DIR ${LIBRARIES_DIR}/zlib)
	add_subdirectory(${LIBRARIES_DIR}/zlib)
endif()

if(CXMF_INCLUDE_IMPORTER)
	set(ASSIMP_BUILD_M3D_IMPORTER OFF)
	set(ASSIMP_BUILD_M3D_EXPORTER OFF)
	set(ASSIMP_BUILD_USD_IMPORTER OFF)
	set(ASSIMP_BUILD_USD_VERBOSE_LOGS OFF)
	set(ASSIMP_BUILD_VRML_IMPORTER OFF)
	set(ASSIMP_BUILD_USE_CCACHE ON)
	set(ASSIMP_HUNTER_ENABLED OFF)
	set(ASSIMP_BUILD_FRAMEWORK OFF)
	set(ASSIMP_DOUBLE_PRECISION OFF)
	set(ASSIMP_OPT_BUILD_PACKAGES OFF)
	set(ASSIMP_ANDROID_JNIIOSYSTEM OFF)
	set(ASSIMP_NO_EXPORT ON)
	set(ASSIMP_BUILD_ASSIMP_TOOLS OFF)
	set(ASSIMP_BUILD_SAMPLES OFF)
	set(ASSIMP_BUILD_TESTS OFF)
	set(ASSIMP_COVERALLS OFF)
	set(ASSIMP_INSTALL OFF)
	set(ASSIMP_WARNINGS_AS_ERRORS ON)
	set(ASSIMP_ASAN OFF)
	set(ASSIMP_UBSAN OFF)
	set(ASSIMP_BUILD_DOCS OFF)
	set(ASSIMP_INJECT_DEBUG_POSTFIX ON)
	set(ASSIMP_IGNORE_GIT_HASH ON)
	set(ASSIMP_BUILD_ZLIB OFF)
	set(ASSIMP_INSTALL_PDB OFF)
	set(ASSIMP_BUILD_ASSIMP_VIEW OFF)
	set(ASSIMP_BUILD_ALL_IMPORTERS_BY_DEFAULT OFF)
	set(ASSIMP_BUILD_ALL_EXPORTERS_BY_DEFAULT OFF)
	set(ASSIMP_BUILD_GLTF_IMPORTER ON)

	set(GLM_BUILD_LIBRARY OFF)
	set(GLM_BUILD_TESTS OFF)
	set(GLM_BUILD_INSTALL OFF)
	set(GLM_ENABLE_CXX_20 ON)

	set(MESHOPT_BUILD_DEMO OFF)
	set(MESHOPT_BUILD_GLTFPACK OFF)
	set(MESHOPT_BUILD_SHARED_LIBS OFF)
	set(MESHOPT_STABLE_EXPORTS OFF)
	set(MESHOPT_WERROR ON)
	set(MESHOPT_INSTALL OFF)

	add_subdirectory(${LIBRARIES_DIR}/assimp)
	add_subdirectory(${LIBRARIES_DIR}/glm)
	add_subdirectory(${LIBRARIES_DIR}/meshoptimizer)
endif()

set(SOURCE_FILES
	${SOURCE_DIR}/CXMF.cpp
)

if(CXMF_IS_STANDALONE_BUILD)
	list(APPEND SOURCE_FILES ${SOURCE_DIR}/main.cpp)
	if(MSVC)
		list(APPEND SOURCE_FILES ${CMAKE_CURRENT_SOURCE_DIR}/resources/resource.rc)
	endif()
	add_executable(cxmf ${SOURCE_FILES})
else()
	add_library(cxmf STATIC ${SOURCE_FILES})
endif()

target_compile_definitions(cxmf PUBLIC
	CXMF_MAX_MESHLET_VERTICES=64
	CXMF_MAX_MESHLET_TRIANGLES=96
)

target_include_directories(cxmf PUBLIC ${INCLUDE_DIR})
target_include_directories(cxmf PRIVATE ${SOURCE_DIR})
target_link_libraries(cxmf PRIVATE ${ZLIB_LIBRARIES})

if(CXMF_INCLUDE_IMPORTER)
	target_compile_definitions(cxmf PRIVATE CXMF_INCLUDE_IMPORTER)
	target_link_libraries(cxmf PRIVATE assimp glm::glm meshoptimizer)
endif()

target_compile_definitions(cxmf PRIVATE
	CXMF_VERSION_MAJOR=${PROJECT_VERSION_MAJOR}
	CXMF_VERSION_MINOR=${PROJECT_VERSION_MINOR}
	CXMF_VERSION_PATCH=${PROJECT_VERSION_PATCH}
)

if(MSVC)
	target_compile_definitions(cxmf PRIVATE
		_CRT_SECURE_NO_WARNINGS=1
		_CRT_NONSTDC_NO_WARNINGS=1
	)
endif()
